model{

	##Population level likelihood
	for ( i in 1:nBirds){
	
		##Individual level likelihood
		for ( j in 1:(nDays-1) ){
		
			for ( k in 1:n ){
  		
				period[ i,j,k ] <- step( tMat[i,j,k] - delta[i,j,1] ) + step( tMat[i,j,k] - delta[i,j, 2] ) + 1 
  		  		yMat[i,j,k] ~ dnorm( m_y[ i, period[ i,j,k ]] , 1/sigma_y[ i, period[ i,j,k ]]^2 )
      			}
    		}
		  
		
	 
		for ( k in 1:n ){

		    period[ i,nDays,k ] <- step( tMat[i,nDays,k] - delta[i,nDays,1] ) + step( tMat[i,nDays,k] - delta[i,nDays, 2] - delta_prime ) + 1 
		    yMat[i,nDays,k] ~ dnorm( m_y[ i, period[ i,nDays,k ]] , 1/sigma_y[ i, period[ i,nDays,k ]]^2 )
	  	}
    	}

	##Population level priors
  	for( i in 1:nBirds){
  
		##Individual level priors
		for( j in 1:3 ){
      		     m_y[i, j ] ~ dnorm( mu_mu[j], 1 / ( sd_mu[j]^2 ) ) 
      		     sigma_y[ i, j] ~ dscaled.gamma( 10, 2 )
  	        }
  	
    		for( j in 1:(nDays) ){
 		     delta[i,j, 1] ~ dnorm( mu_delta[i,1], tau_delta1 ) 
		     delta[i,j, 2] ~ dnorm( mu_delta[i,2],  tau_delta2 )
		}

		##delta[i,nDays, 1] ~ dnorm( mu_delta[i, 1], tau_delta1)
		##delta[i,nDays, 2] ~ dnorm( mu_delta[i, 2] + delta_prime , tau_delta2 )

		mu_delta[i,1] ~ dnorm(mu_mu_delta[1], tau_mu_delta1)
		mu_delta[i,2] ~ dnorm(mu_mu_delta[2], tau_mu_delta2)
  	}

	
	
	delta_prime ~ dnorm( mu_delta_prime, 1 / (sigma_delta_prime^2) )

	mu_mu_delta[1] ~ dnorm(mu_mu_mu_delta[1], tau_mu_mu_delta1)
	mu_mu_delta[2] ~ dnorm(mu_mu_mu_delta[2], tau_mu_mu_delta2)

	tau_mu_mu_delta1 ~ dscaled.gamma(1,2)
	tau_mu_mu_delta2 ~ dscaled.gamma(1,2)
	
	tau_mu_delta1 ~ dscaled.gamma(1,2)
	tau_mu_delta2 ~ dscaled.gamma(1,2)

	tau_delta1 ~ dscaled.gamma(1, 2)
	tau_delta2 ~ dscaled.gamma(1, 2)
}
