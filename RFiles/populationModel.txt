model{

	##Population level likelihood
	for ( i in 1:nBirds){
			
		##Individual level likelihood
	  	for ( j in 1:n ){
	    
			period[ i, j ] <- step( t[i, j ] - delta[i, 1] ) + step( t[ i, j ] - delta[i, 2] ) + 1 
		  	y[ i, j ] ~ dnorm( m_y[ i, period[i, j ]] , 1/sigma_y[ i, period[i, j]]^2 )
		  
	  	}
	}

	##Population level priors
  	for( i in 1:nBirds){
  
		##Individual level priors
    		for( j in 1:3 ){
          		m_y[i, j ] ~ dnorm( mu_mu[j], 1 / ( sd_mu[j]^2) ) 
          		sigma_y[ i, j] ~ dscaled.gamma( 10, 2 )
    		}
    		
 		delta[i, 1] ~ dnorm( mu_delta[1], tau_delta1 ) 
		delta[i, 2] ~ dnorm( mu_delta[2], tau_delta2 )
		
	}
	
	mu_delta[1] ~ dnorm( mu_mu_delta[1], tau_mu_delta1 )
	mu_delta[2] ~ dnorm( mu_mu_delta[2], tau_mu_delta2 )
	
	tau_mu_delta1 <- 1 / sd_mu_delta[1]^2
	tau_mu_delta2 <- 1 / sd_mu_delta[2]^2

	tau_delta1 ~ dscaled.gamma(1, 2)
	tau_delta2 ~ dscaled.gamma(1, 2)
}
